model{

############################
# this model is a static mixture model
# where mh1 and vy1 uses the last entry (all data)
############################

for(i in 1:N){

##############################################
# within level
##############################################
  #############################
  for(j in 1:25){
    y[i,j] ~ dnorm(muy[i,j,Cend[i]],psiy[j,Cend[i]])
  }
  #############################
  # scaling constraint ly=1, ty=0
  muy[i,1,1]   <- xi[i,1]
  muy[i,11,1]  <- xi[i,2]
  muy[i,19,1]  <- xi[i,3]
  
  for(j in 2:10) {muy[i,j,1] <- ty1[j-1] + ly[j-1]*xi[i,1]}
  for(j in 12:18){muy[i,j,1] <- ty1[j-2] + ly[j-2]*xi[i,2]}
  for(j in 20:25){muy[i,j,1] <- ty1[j-3] + ly[j-3]*xi[i,3]}

  # rest has random mean
  for(j in 1:25) {muy[i,j,2] <- ty2[j]}

  #############################

  #############################
  # factors
  xi[i,1:3] ~ dmnorm(muxi,psixi)
  #############################
  

  # time invariant covariate on L1
  logit(eta1[i]) <- b0[1]+b0[2]*vy[i]+b0[3]*cd[i]

  PC1[i,1] <- eta1[i]
  PC1[i,2] <- 1-PC1[i,1]
    
  Cend[i] ~ dcat(PC1[i,1:2]) 

}

##############################################
# priors 
##############################################

for(j in 1:25){
  psiy[j,1] ~ dgamma(9,4)
  psiy[j,2] ~ dgamma(9,4)
  ty1[j] ~ dnorm(0,1) # this is a difference to the factor mean
  ty2[j] ~ dnorm(0,1) # mean of vars can be anything
  ly[j]  ~ dnorm(1,1)I(0,)
}

for(j in 1:3){muxi[j] = 0}

psixi[1:3,1:3] ~ dwish(R0[1:3,1:3],3)

# is censoring correct?
b0[1]~dnorm(0,1e-1)
b0[2]~dnorm(0,1e-1)
b0[3]~dnorm(0,1e-1)I(,0)

#############################

##############################################
# transformations
##############################################
for(j in 1:25){sigmay[j]<- 1/psiy[j,1]}
sigmaxi[1:3,1:3] <- inverse(psixi[1:3,1:3])


}

